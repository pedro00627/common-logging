plugins {
    id 'java-library'
    id 'maven-publish'
}

import org.gradle.api.publish.maven.MavenPublication
import org.gradle.jvm.toolchain.JavaLanguageVersion
import org.gradle.jvm.toolchain.JvmVendorSpec

group = 'com.github.pedro00627'
version = '1.0.16'
description = 'Universal logging configuration library for Spring Boot applications'

repositories {
    mavenCentral()
}

dependencies {
    api platform('org.springframework.boot:spring-boot-dependencies:3.2.5')

    api 'org.apache.logging.log4j:log4j-api'
    api 'org.apache.logging.log4j:log4j-core'
    api 'org.apache.logging.log4j:log4j-layout-template-json'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
        vendor.set(JvmVendorSpec.ADOPTIUM)
    }
    withSourcesJar()
    withJavadocJar()
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(tasks.named('jar'))
            artifact(tasks.named('sourcesJar'))
            artifact(tasks.named('javadocJar'))

            pom {
                name.set('Common Logging')
                description.set('Universal logging configuration library for Spring Boot applications')
                url.set('https://github.com/pedro00627/common-logging')

                licenses {
                    license {
                        name.set('MIT License')
                        url.set('https://opensource.org/licenses/MIT')
                    }
                }

                developers {
                    developer {
                        id.set('pedro00627')
                        name.set('Pedro')
                        email.set('pedro00627@users.noreply.github.com')
                    }
                }

                scm {
                    connection.set('scm:git:git://github.com/pedro00627/common-logging.git')
                    developerConnection.set('scm:git:ssh://github.com/pedro00627/common-logging.git')
                    url.set('https://github.com/pedro00627/common-logging')
                }
            }
        }
    }

    repositories {
        maven {
            name = "localMaven"
            url = uri(new File(System.getProperty('user.home'), '.m2/repository').toURI())
        }
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/pedro00627/common-logging")
            credentials { creds ->
                creds.username = System.getenv("GITHUB_ACTOR")
                creds.password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}